# Core GDB configuration file.
#
# This will be sourced before any other configuration file and should define the
# configuration options and helper functions needed by the rest of the configs.

###############################################################################
#                                 GDB Options                                 #
###############################################################################

set confirm off
set verbose off
set history filename ~/.cache/gdb_history
set history save
set logging file ~/.local/share/gdb.log
set startup-quietly on
set debuginfod enabled off

# These make gdb never pause in its output
set pagination off
set height 0
set width 0

###############################################################################
#                            Customization Options                            #
###############################################################################

# Whether to use colorized output or not
set $USECOLOR = 1

###############################################################################
#                               Color Functions                               #
###############################################################################

# Color codes
set $BLACK = 0
set $RED = 1
set $GREEN = 2
set $YELLOW = 3
set $BLUE = 4
set $MAGENTA = 5
set $CYAN = 6
set $WHITE = 7

# This is ugly but there's no else if available :-(
define color
 if $USECOLOR == 1
   # BLACK
   if $arg0 == 0
     echo \033[30m
   else
     # RED
     if $arg0 == 1
       echo \033[31m
     else
       # GREEN
       if $arg0 == 2
         echo \033[32m
       else
         # YELLOW
         if $arg0 == 3
           echo \033[33m
         else
           # BLUE
           if $arg0 == 4
             echo \033[34m
           else
             # MAGENTA
             if $arg0 == 5
               echo \033[35m
             else
               # CYAN
               if $arg0 == 6
                 echo \033[36m
               else
                 # WHITE
                 if $arg0 == 7
                   echo \033[37m
                 end
               end
             end
           end
         end
       end
     end
   end
 end
end

define color_reset
  if $USECOLOR == 1
    echo \033[0m
  end
end

define color_bold
  if $USECOLOR == 1
    echo \033[1m
  end
end

define color_underline
  if $USECOLOR == 1
    echo \033[4m
  end
end
